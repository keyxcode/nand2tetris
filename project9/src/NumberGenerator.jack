class NumberGenerator {
    // pseudo random number generator using LCG algo with small constants
    // https://en.wikipedia.org/wiki/Linear_congruential_generator

    field int seed;
    field int m; // modulus
    field int a; // multiplier
    field int c; // increment

    constructor NumberGenerator new(int userSeed) {
        let seed = userSeed;
        let m = 256;
        let a = 75;
        let c = 7;

        return this;
    }

    method int next() {
        let seed = Utils.mod(((a * seed) + c), m);

        return seed;
    }

    method int generate(int min, int max) {
        // generate random value within [min, max]
        var int range;
        let range = max - min + 1;
        return min + Utils.mod(next(), range);
    }
}
