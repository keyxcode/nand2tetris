// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/a/PC.hdl
/**
 * A 16-bit counter.
 * if      reset(t): out(t+1) = 0
 * else if load(t):  out(t+1) = in(t)
 * else if inc(t):   out(t+1) = out(t) + 1
 * else              out(t+1) = out(t)
 */
CHIP PC {
    IN in[16], reset, load, inc;
    OUT out[16];
    
    PARTS:
    Or(a=load, b=reset, out=loadOrReset);
    Or(a=loadOrReset, b=inc, out=registerLoad);
    
    Inc16(in=outRegister, out=outInc);
    
    Mux16(a=outInc, b=in, sel=load, out=incMuxLoad);
    Mux16(a=incMuxLoad, b=false, sel=reset, out=inRegister);

    Register(in=inRegister, load=registerLoad, out=outRegister, out=out);
}